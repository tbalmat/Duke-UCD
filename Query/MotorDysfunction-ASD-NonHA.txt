#############################################################################################

Motor dysfunction query:

match  (prt:Participant)-[:P_SCT]->(sct0:ObjectConcept)-[rsct:ISA*]->(sct2:ObjectConcept)
where  sct2.sctid in['52559000'] and sct0.active='1' and sct2.active='1'
       and reduce(n=0, r in rsct|n+case when(r.active='0')then 1 else 0 end)=0

with   prt, sct0, startNode(last(rsct)) as sct1
where  sct1.active='1'

optional match(prt)-[:P_RX]->(rx0:RXCUI)
where  toLower(rx0.status)='active'

optional match(rx0)<-[:SUBSUMES]-(rx1:RXCUI)
where    toLower(rx1.status)='active'

with     prt, sct0, sct1, rx0, rx1
optional match(sct0)-[role1:HAS_ROLE_GROUP]->(rg)-[role2:FINDING_SITE]->(fst:ObjectConcept)
where  role2.active='1' and fst.active='1'

with     distinct   prt.ParticipantId as participantID,   case when(labels(prt)=['Participant'])then 'UCDDist'        when(labels(prt)=['Participant','UCD_Proximal'])then 'UCDProx'        else 'na'   end as UCDProxDist,   prt.Sex as Sex, prt.UCDDx as UCDDx, prt.HASxLast as HASxLast,   case when(toInteger(prt.OnsetAgeDays)<11)then '0-11'        when(toInteger(prt.OnsetAgeDays)<101)then '11-100'        when(toInteger(prt.OnsetAgeDays)<1001)then '101-1000'        when(toInteger(prt.OnsetAgeDays)<10001)then '1001-10000'        when(toInteger(prt.OnsetAgeDays) is not null)then '>10000'        else null   end as onsetAgeDays,   sct1.sctid as conceptID, sct1.FSN as conceptFSN,   coalesce(rx1.id, rx0.id) as rxSubsID, coalesce(rx1.name, rx0.name) as rxSubsName,   rx0.id as rxID, rx0.name as rxName,   fst.sctid as findingSiteID, fst.FSN as findingSiteFSN, type(role2) as fsRole

where 1=1
return   participantID, coalesce(UCDProxDist, 'na') as UCDProxDist,   coalesce(Sex, 'na') as Sex, coalesce(HASxLast, 'na') as HASxLast,   coalesce(UCDDx, 'na') as UCDDx, coalesce(onsetAgeDays, 'na') as onsetAgeDays,   conceptID, conceptFSN,   coalesce(rxSubsID, 'na') as rxSubsID, coalesce(rxSubsName, 'na') as rxSubsName,   coalesce(rxID, 'na') as rxID, coalesce(rxName, 'na') as rxName,   coalesce(findingSiteID, 'na') as findingSiteID, coalesce(findingSiteFSN, 'na') as findingSiteFSN,   coalesce(fsRole, 'na') as fsRole

#############################################################################################

Confirm that non-existence of ASD combined with non-HA events and motor dysfunction

match  (prt:Participant)-[:P_SCT]->(sct0:ObjectConcept)-[rsct:ISA*]->(sct2:ObjectConcept)
where  sct2.sctid in['52559000'] and sct0.active='1' and sct2.active='1'
       and reduce(n=0, r in rsct|n+case when(r.active='0')then 1 else 0 end)=0
return prt.HASxLast, prt.UCDDx
order by prt.UCDDx

Result:  all ASD participants have HA events

#############################################################################################
